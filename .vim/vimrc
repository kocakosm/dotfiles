"------------------------------"
"---------  Settings  ---------"
"------------------------------"

" Set UTF-8 as the default encoding
set encoding=utf-8

" Do not recognize octal numbers for Ctrl-A and Ctrl-X
set nrformats-=octal

" Consider numbers as unsigned
set nrformats+=unsigned

" Disable modeline
set nomodeline

" Do not apply the langmap option to characters that result from a mapping
set nolangremap

" Ignore mappings and options for :mksession and :mkview
set sessionoptions-=options
set viewoptions-=options

" Enable the mouse
set mouse=a

" Set cursor shapes for insert, replace and normal modes
if !has('gui_running')
  let &t_SI="\<Esc>[5 q"
  let &t_SR="\<Esc>[3 q"
  let &t_EI="\<Esc>[1 q"
endif

" Automatically reload files when modified by an external program
set autoread

" Keep 250 lines of command line history
set history=250

" Reduce delay when using escape key in a terminal
set ttimeout
set ttimeoutlen=50

" Milliseconds to wait after nothing is typed to write the swap file and fire the CursorHold event
set updatetime=125

" Allow more than one unsaved buffer
set hidden

" Go to the last used window when jumping to errors with quickfix commands
set switchbuf=uselast

" Enhanced command-line completion
set wildmenu
set wildmode=longest,full
set wildoptions=pum

" Case insensitive command-line completion of file names and directories
set wildignorecase

" Ignore files/directories matching these patterns
set wildignore+=*/.hg/**,*/.git/**
set wildignore+=*.tags,*.orig
set wildignore+=*.tar*,*.zip,*.jar,*.war
set wildignore+=*.png,*.jpg,*.jpeg,*.gif
set wildignore+=*.o,*.pyc,*.pyo,*.class,*.min.*
set wildignore+=*.pdf,*.aux,*.log,*.dvi,*.bbl,*.blg,*.out,*.toc

" Save undo tree to disk
set undofile

" Do not create a swap file by default
set noswapfile

" Do not flush the swap file to disk every time it is written
set swapsync=

" Put all temporary files in a more sensible place
call system#mkdir(system#user_data_dir() . 'vim/swap')
call system#mkdir(system#user_data_dir() . 'vim/undo')
call system#mkdir(system#user_data_dir() . 'vim/info')
call system#mkdir(system#user_data_dir() . 'vim/backup')
let &backupdir=system#user_data_dir() . 'vim/backup/'
let &directory=system#user_data_dir() . 'vim/swap//'
let &undodir=system#user_data_dir() . 'vim/undo/'
let &viminfo="'100,n" . system#user_data_dir() . 'vim/info/viminfo'

" Set the window title
set title
set titlestring=%F\ %m

" Enable true colors support in terminal
set termguicolors

" Don't display the intro message
set shortmess+=I

" Don't give the file info when editing a file
set shortmess+=F

" Don't show 'search hit bottom, continuing at top' messages
set shortmess+=s

" Don't show insert mode completion menu messages
set shortmess+=c

" Don't show 'scanning tags' messages
set shortmess+=C

" Display commands as they are typed
set showcmd

" Always show the status line
set laststatus=2

" Show line numbers
set number

" Use 3 columns for line numbers
set numberwidth=3

" Highlight the line number of the current line
set cursorline
set cursorlineopt=number

" Prettier vertical separators, fold markers...
let &fillchars='vert:│,diff: ,fold: ,foldclose:▸,foldopen:▾,foldsep:│,eob:-,lastline:.'

" Open new windows below the current one
set splitbelow

" Open new windows at the right-hand side of the current one
set splitright

" Stabilize the topline when opening/closing a (horizontal) window
set splitkeep=topline

" Maximum text width (0 means don't cut lines)
set textwidth=0

" Highlight column after 'textwidth' (when > 0)
set colorcolumn=+1

" Don't wrap long lines by default
set nowrap

" Add a ↳ at the start of lines that have been wrapped
let &showbreak='↳ '

" Visually indent wrapped lines
set breakindent

" Shift wrapped line's beginning by 1 character
set breakindentopt=shift:1

" Only wrap at a character in the 'breakat' option
set linebreak

" Do not insert two spaces after a '.', '?' and '!' with a join command
set nojoinspaces

" Show as much as possible of the last line
set display=lastline

" Highlight matching brackets
set showmatch
set matchtime=3

" Disable folding by default
set nofoldenable

" Try to keep 3 lines above and below the cursor
set scrolloff=3

" Side-scroll 5 characters before the screen border
set sidescrolloff=5

" Side-scroll 1 character at a time
set sidescroll=1

" Try to keep the cursor at the current column when jumping to other lines
set nostartofline

" Strings to use in 'list' mode and for the ':list' command
let &listchars='extends:…,precedes:…'

" More powerful backspacing in insert mode
set backspace=indent,eol,start

" Allow <left>/<right> and h/l to wrap the cursor around line borders
set whichwrap=<,>,h,l,[,]

" Enable virtual editing in visual block mode
set virtualedit=block

" Automatic indentation
set autoindent

" Expand tabulations to spaces
set expandtab

" Use 4 characters-wide tabulations by default
set tabstop=4
set smarttab

" Use 4 characters as indentation level by default
set shiftwidth=4

" >> indents to next multiple of 'shiftwidth'
set shiftround

" Files to scan for <c-x><c-k> insert mode completions
set dictionary=/usr/share/dict/british-english
set dictionary+=/usr/share/dict/american-english
set dictionary+=/usr/share/dict/french

" Places to scan for <c-n>, <c-p> and <c-x><c-l> insert mode completions
set complete=.,w,b,u,t,i

" Insert mode completion options
set completeopt=menuone,popup
set completepopup+=border:off

" Don't show more than 15 items in the popup menu
set pumheight=15

" Automatic formatting options
set formatoptions=jncql

" Use system's default clipboards for yanking/pasting
set clipboard=unnamedplus,unnamed

" Case insensitive search
set ignorecase

" Smart case sensitivity (has priority over ignorecase)
set smartcase

" Enable incremental search
set incsearch

" Start diff mode with vertical splits
set diffopt+=vertical

" Use the internal diff with the histogram algorithm
set diffopt+=internal,algorithm:histogram

" Practically disable folding in diff mode
set diffopt+=context:999999

" When spell-checking a camel cased word, every upper-case character that
" comes after a lower case character is treated as the start of a new word
set spelloptions=camel

" Enable file type detection
filetype plugin indent on

" Enable syntax highlighting
syntax on

"----------------------------------"
"---------  Autocommands  ---------"
"----------------------------------"

augroup Vimrc
  autocmd!
augroup END

" Command-line window customization
autocmd Vimrc CmdwinEnter [:/?] setlocal nonumber textwidth=0

" Preview window customization
autocmd Vimrc WinEnter * if &previewwindow | setlocal nonumber | endif

" Make read-only mode really read-only
autocmd Vimrc OptionSet readonly let &l:modifiable=!&readonly
autocmd Vimrc BufEnter * ++once let &l:modifiable=!&readonly

"------------------------------"
"---------  Mappings  ---------"
"------------------------------"

" Use <space> as leader (this doesn't change the leader key, just maps <space> to it)
map <space> <leader>

" Finer-grained undo
inoremap <silent> <tab> <tab><c-g>u
inoremap <silent> <space> <space><c-g>u
inoremap <silent> <cr> <cr><c-g>u
inoremap <silent> <c-u> <c-g>u<c-u>
inoremap <silent> <c-w> <c-g>u<c-w>

" Y yanks from the cursor to the end of the line
nnoremap <silent> Y y$

" Do not move the cursor after a visual yank
vnoremap <silent> <expr> y 'my"' . v:register . 'y`y'

" Duplicate the current line
nnoremap <silent> <c-s-up> <cmd>call buffer#duplicate_line_up()<cr>
nnoremap <silent> <c-s-down> <cmd>call buffer#duplicate_line_down()<cr>
inoremap <silent> <c-s-up> <cmd>call buffer#duplicate_line_up()<cr>
inoremap <silent> <c-s-down> <cmd>call buffer#duplicate_line_down()<cr>

" Move current/secleted line(s) up/down
nnoremap <silent> <a-s-down> :silent! move .+1<cr>==
nnoremap <silent> <a-s-up> :silent! move .-2<cr>==
inoremap <silent> <a-s-down> <esc>:silent! move .+1<cr>==gi
inoremap <silent> <a-s-up> <esc>:silent! move .-2<cr>==gi
xnoremap <silent> <a-s-down> :silent! move '>+1<cr>gv=gv
xnoremap <silent> <a-s-up> :silent! move '<-2<cr>gv=gv

" <tab> / <shift-tab> navigates between windows in normal mode
nnoremap <silent> <tab> <C-w>w
nnoremap <silent> <s-tab> <C-w>W

" <alt>-<right> switches to the next buffer
noremap <silent> <a-right> <cmd>bn!<cr>
inoremap <silent> <a-right> <cmd>bn!<cr>

" <alt>-<left> switches to the previous buffer
noremap <silent> <a-left> <cmd>bp!<cr>
inoremap <silent> <a-left> <cmd>bp!<cr>

" Like 'p' but leaves the cursor after the newly pasted text
nnoremap <leader>gp gp

" gp reselects last pasted text
nnoremap <silent> <expr> gp '`[' . strpart(getregtype(), 0, 1) . '`]'

" Don't lose visual selection when shifting sidewards
xnoremap <silent> < <gv
xnoremap <silent> > >gv

" <ctrl-l> picks the first suggestion for the last spelling mistake
inoremap <silent> <c-l> <c-g>u<esc>[s1z=`]a<c-g>u
nnoremap <silent> <c-l> [s1z=`]

" Easily replace the word under the cursor or the current visual selection
nnoremap <leader>r :% s/\<<c-r><c-w>\>//g<left><left>
xnoremap <leader>r y:% s/<c-r>"//g<left><left>

" Easier buffer switching
" nnoremap <leader>l <cmd>ls<cr>:buffer<space>

" Quit using <c-q>
noremap <silent> <c-q> <cmd>q<cr>
inoremap <silent> <c-q> <cmd>q<cr>
cnoremap <silent> <c-q> <cmd>q<cr>

" Insert mode completion mappings
imap <c-@> <c-space>
inoremap <c-space> <c-x><c-o>
call keymap#conditional_map('i', 'pumvisible()', '<c-space>', '<c-n>')
call keymap#conditional_map('i', 'pumvisible()', '<down>', '<c-n>')
call keymap#conditional_map('i', 'pumvisible()', '<up>', '<c-p>')
call keymap#conditional_map('i', 'pumvisible()', '<esc>', '<c-e>')
call keymap#conditional_map('i', 'pumvisible()', '<cr>', '<c-y>')

"--------------------------------------"
"---------  Built-in plugins  ---------"
"--------------------------------------"

" Prevent unused built-in plugins from being loaded
let g:loaded_2html_plugin=1
let g:loaded_getscriptPlugin=1
let g:loaded_logiPat=1
let g:loaded_netrwPlugin=1
let g:loaded_rrhelper=1
let g:loaded_vimballPlugin=1
let g:loaded_gzip=1
let g:loaded_zipPlugin=1
let g:loaded_tarPlugin=1

" Load built-in EditorConfig plugin
packadd! editorconfig

" Load man pages viewer
if exists(':Man') != 2
  runtime ftplugin/man.vim
endif
cnoreabbrev <expr> man (getcmdtype() ==# ':' && getcmdline() ==# 'man') ? 'Man' : 'man'

" Emulate netrw's gx
nnoremap <silent> gx <cmd>call system#open(expand('<cWORD>'))<cr>
xnoremap <silent> gx <cmd>call system#open(getregion('v', '.', mode())->filter({_, v -> match(v, '^\s*$') < 0})->get(0, ''))<cr>

"--------------------------------------"
"---------  Personal plugins  ---------"
"--------------------------------------"

" Vstats configuration
xmap <silent> ++ <plug>(vstats)
nmap <silent> ++ ggVG<plug>(vstats)

" Zoom configuration
nmap <silent> <c-w>o <plug>(zoom#toggle)
nmap <silent> <c-w><c-o> <plug>(zoom#toggle)

" Sticky-buffers configuration
let g:sticky_buffers_ignored_buftypes=['help']
let g:sticky_buffers_ignored_filetypes=['dirvish']

" Popterm configuration
nnoremap <silent> <f4> <cmd>PopTermToggle<cr>
inoremap <silent> <f4> <cmd>PopTermToggle<cr>
tnoremap <silent> <f4> <cmd>PopTermToggle<cr>

" Cooler configuration
set hlsearch
nnoremap <silent> <esc> <cmd>nohlsearch<cr>

" Skyline configuration
set noshowmode
let g:skyline = #{
\  components: #{
\    Spacer: '%=',
\    Separator: '::',
\    Mode: ' %{statusline#Mode()} ',
\    BufferType: ' %{statusline#BufferType()} ',
\    GitHead: ' %{statusline#GitHead()} ',
\    SpellLang: ' %{statusline#SpellLang()} ',
\    FileInfo: ' %{statusline#FileInfo()} ',
\    Filename: ' %{statusline#Filename()} ',
\    Bufname: ' %{bufname()} ',
\    FileFormat: ' %{statusline#FileFormat()} ',
\    LineInfo: ' %12(%l/%L %p%%%) ',
\    Ruler: ' %16(%l/%L:%02.v %p%%%) ',
\    Percentage: ' %P ',
\    QuickfixTitle: ' %{statusline#QuickfixTitle()} '
\  },
\  statuslines: {
\    '': #{
\      active: ['Mode', 'GitHead', 'SpellLang', 'Spacer', 'FileInfo', 'Separator', 'FileFormat', 'Spacer', 'Ruler'],
\      inactive: ['Spacer', 'FileInfo', 'Spacer']
\    },
\    'terminal': #{default: ['BufferType', 'Spacer', 'Bufname', 'Spacer']},
\    'help': #{default: ['BufferType', 'Spacer', 'Filename', 'Spacer', 'LineInfo']},
\    'qf': #{default: ['BufferType', 'Spacer', 'QuickfixTitle', 'Spacer', 'LineInfo']},
\    'man': #{default: ['BufferType', 'Spacer', 'Filename', 'Spacer', 'Percentage']},
\    'nerdtree': #{default: ['BufferType']},
\    'vim-plug': #{default: ['BufferType']},
\    'fugitive': #{default: ['BufferType', 'GitHead']},
\    'dirvish': #{default: ['BufferType', 'Spacer', 'Bufname', 'Spacer', 'LineInfo']}
\  }
\}

" Spyglass configuration
nnoremap <leader>f <cmd>Files<cr>
nnoremap <leader><space> <cmd>Buffers<cr>

"-----------------------------------------"
"---------  Third-party plugins  ---------"
"-----------------------------------------"

call plug#begin(system#user_data_dir() . 'vim/plugins')
Plug 'airblade/vim-rooter'
Plug 'alvan/vim-closetag', {'for': ['xml', 'xsd', 'html', 'xhtml']}
Plug 'andymass/vim-matchup'
" Plug 'chaoren/vim-wordmotion'
" Plug 'chrisbra/NrrwRgn', {'on': '<plug>NrrwrgnDo'}
Plug 'flwyd/vim-conjoin'
" Plug 'hrsh7th/vim-vsnip'
" Plug 'jiangmiao/auto-pairs'
Plug 'justinmk/vim-dirvish'
Plug 'kocakosm/hilal'
" Plug 'lervag/vimtex', {'for': ['tex']}
Plug 'ludovicchabant/vim-gutentags'
" Plug 'mg979/vim-visual-multi'
Plug 'mhinz/vim-signify'
" Plug 'natebosch/vim-lsc'
" Plug 'preservim/nerdtree', {'tag': '*', 'on': 'NERDTreeToggle'}
" Plug 'shime/vim-livedown', {'for': ['markdown'], 'do': 'npm -g install livedown'}
Plug 'haya14busa/vim-asterisk'
" Plug 'tommcdo/vim-exchange'
Plug 'tommcdo/vim-lion'
Plug 'tpope/vim-commentary'
" Plug 'tpope/vim-fugitive'
" Plug 'wellle/targets.vim'
" Plug 'zirrostig/vim-schlepp', {'on': '<plug>Schlepp'}
call plug#end()

for s:plugin in keys(g:plugs)
  let s:plugin_name = substitute(tolower(s:plugin), '^\(n\)\?vim-', '', '')
  let s:plugin_name = substitute(s:plugin_name, '[\.-]\(n\)\?vim$', '', '')
  let s:plugin_conf = system#user_vim_dir() . 'plugins-config/' . s:plugin_name . '.vim'
  if filereadable(s:plugin_conf)
    execute 'source ' . s:plugin_conf
  endif
endfor
